var Number setCurrentTemp = 17
var Number antiFreezeTemp = 7
var Number defaultWinterTemp = 20

val analyzeTemperature = [ GenericItem pump, GenericItem temperatureItem, Number currentSetpoint | 
    val currentTemp = temperatureItem.state

    var pumpState = pump.state
    logInfo("temperature", "PUMP State: {}, setCurrentTemp: {},  CurrentTemp: {}", pumpState, currentSetpoint, currentTemp)

    if(currentTemp < currentSetpoint && pumpState == OFF) {
        logInfo("temperature", "Switching ON PUMP.")
        sendCommand(pump, ON)
    } else if (currentTemp > currentSetpoint && pumpState == ON) {
        logInfo("temperature", "Switching OFF PUMP.")
        sendCommand(pump, OFF)
    } else {
        logInfo("temperature", "No Temp action taken.")
    }
]

val updateSetpoint = [ GenericItem setpointShiftItem, Number currentSetpoint | 
    val setIncreaseOrDecrease = setpointShiftItem.state
    logInfo("temperature", "Update Setpoint: {}", setIncreaseOrDecrease)

    if(setIncreaseOrDecrease == 1) {
        currentSetpoint + 0.1
    } else {
        currentSetpoint - 0.1
    }
]

rule "KNX Send Day/Night"
when
	Item GF_SWITCH_LIVING_DATETIME received update
then
    val day_start = (SunriseTime.state as DateTimeType).getZonedDateTime()
    val evening_start = (SunsetTime.state as DateTimeType).getZonedDateTime()

    if (now.isAfter(evening_start) || now.isBefore(day_start)) {
        sendCommand(DayModeSwitch, OFF)
    } else {
        sendCommand(DayModeSwitch, ON)
    }
end

rule "FF temperature updated"
when
	Item FF_FamilyRoom_Temperature received update
then
    analyzeTemperature.apply(HEAT_FFPump, FF_FamilyRoom_Temperature, setCurrentTemp)
end

rule "FF temperature SetPoint"
when
	Item FF_Temp_Setpoint_Shift received update
then
    setCurrentTemp = updateSetpoint.apply(FF_Temp_Setpoint_Shift, setCurrentTemp)
    logInfo("temperature", "FF Setpoint: {}", setCurrentTemp)
    sendCommand(FF_Heating, setCurrentTemp)
end


rule "FF Update Current Setpoint"
when
	Item FF_Heating received update
then
    setCurrentTemp = FF_Heating.state
    logInfo("temperature", "FFTemperature change: {}", setCurrentTemp)
    sendCommand(FF_Temp_Current_Setpoint, setCurrentTemp)
    analyzeTemperature.apply(HEAT_FFPump, FF_FamilyRoom_Temperature, setCurrentTemp)
end

rule "GF temperature updated"
when
	Item GF_Temperature received update
then
    analyzeTemperature.apply(HEAT_GFPump, GF_Temperature, setCurrentTemp)
end

rule "GF temperature SetPoint"
when
	Item GF_Temp_Setpoint_Shift received update
then

    setCurrentTemp = updateSetpoint.apply(GF_Temp_Setpoint_Shift, setCurrentTemp)
    logInfo("temperature", "GF Setpoint: {}", setCurrentTemp)
    sendCommand(GF_Heating, setCurrentTemp)
end

rule "GF Update Current Setpoint"
when
	Item GF_Heating received update
then
    setCurrentTemp = GF_Heating.state
    logInfo("temperature", "DFTemperature change: {}", setCurrentTemp)

    sendCommand(GF_Temp_Current_Setpoint, setCurrentTemp)
    analyzeTemperature.apply(HEAT_GFPump, GF_Temperature, setCurrentTemp)
end

rule "Antifreeze update"
when
	Item HEAT_AntiFreeze received update
then
    if(HEAT_AntiFreeze.state == ON) {
        logInfo("temperature", "AntiFreeze is ON. Setting temp to: {}", antiFreezeTemp)
        sendCommand(GF_Heating, antiFreezeTemp)
        sendCommand(FF_Heating, antiFreezeTemp)
    } else {
        logInfo("temperature", "AntiFreeze is OFF. Setting temp to: {}", setCurrentTemp)
        sendCommand(GF_Heating, defaultWinterTemp)
        sendCommand(FF_Heating, defaultWinterTemp)
    }
end

rule "Check Heating items"
when
	Time cron "0 0/1 * * * ?"
then
    if(HEAT_AntiFreeze.state == ON) {
        logInfo("temperature", "AntiFreeze is ON. Setting temp to: {}", antiFreezeTemp)
        sendCommand(GF_Heating, antiFreezeTemp)
        sendCommand(FF_Heating, antiFreezeTemp)
    } else {
        if(GF_Heating.state == NULL || GF_Heating.state < setCurrentTemp) {
            logInfo("temperature", "GF_Heating is NULL. Setting temp to: {}", setCurrentTemp)
            sendCommand(GF_Heating, setCurrentTemp)
        }

        if(FF_Heating.state == NULL || GF_Heating.state < setCurrentTemp) {
            logInfo("temperature", "FF_Heating is NULL. Setting temp to: {}", setCurrentTemp)
            sendCommand(FF_Heating, setCurrentTemp)
        }
    }
end