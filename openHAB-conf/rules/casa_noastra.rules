var Number setFFTemp = 17
var Number setGFTemp = 17

val analyzeTemperature = [ GenericItem pump, GenericItem temperatureItem, Number currentSetpoint | 
    val currentTemp = temperatureItem.state

    var pumpState = pump.state
    logInfo("temperature", "PUMP State: {}, setFFTemp: {},  CurrentTemp: {}", pumpState, currentSetpoint, currentTemp)

    if(currentTemp < currentSetpoint && pumpState == OFF) {
        logInfo("temperature", "Switching ON PUMP.")
        sendCommand(pump, ON)
    } else if (currentTemp > currentSetpoint && pumpState == ON) {
        logInfo("temperature", "Switching OFF PUMP.")
        sendCommand(pump, OFF)
    } else {
        logInfo("temperature", "No Temp action taken.")
    }
]

val updateSetpoint = [ GenericItem setpointShiftItem, Number currentSetpoint | 
    val setIncreaseOrDecrease = setpointShiftItem.state
    logInfo("temperature", "Update Setpoint: {}", setIncreaseOrDecrease)

    if(setIncreaseOrDecrease == 1) {
        currentSetpoint + 0.1
    } else {
        currentSetpoint - 0.1
    }
]

rule "KNX Send Day/Night"
when
	Item GF_SWITCH_LIVING_DATETIME received update
then
    val day_start = (SunriseTime.state as DateTimeType).getZonedDateTime()
    val evening_start = (SunsetTime.state as DateTimeType).getZonedDateTime()

    if (now.isAfter(evening_start) || now.isBefore(day_start)) {
        sendCommand(DayModeSwitch, OFF)
    } else {
        sendCommand(DayModeSwitch, ON)
    }
end

rule "FF temperature updated"
when
	Item FF_FamilyRoom_Temperature received update
then
    analyzeTemperature.apply(HEAT_FFPump, FF_FamilyRoom_Temperature, setFFTemp)
end

rule "FF temperature SetPoint"
when
	Item FF_Temp_Setpoint_Shift received update
then
    setFFTemp = updateSetpoint.apply(FF_Temp_Setpoint_Shift, setFFTemp)
    logInfo("temperature", "FF Setpoint: {}", setFFTemp)
    sendCommand(FF_Heating, setFFTemp)
end


rule "FF Update Current Setpoint"
when
	Item FF_Heating received update
then
    setFFTemp = FF_Heating.state
    logInfo("temperature", "FFTemperature change: {}", setFFTemp)
    sendCommand(FF_Temp_Current_Setpoint, setFFTemp)
    analyzeTemperature.apply(HEAT_FFPump, FF_FamilyRoom_Temperature, setFFTemp)
end

rule "GF temperature updated"
when
	Item GF_Temperature received update
then
    analyzeTemperature.apply(HEAT_GFPump, GF_Temperature, setGFTemp)
end

rule "GF temperature SetPoint"
when
	Item GF_Temp_Setpoint_Shift received update
then

    setGFTemp = updateSetpoint.apply(GF_Temp_Setpoint_Shift, setGFTemp)
    logInfo("temperature", "GF Setpoint: {}", setGFTemp)
    sendCommand(GF_Heating, setGFTemp)
end

rule "GF Update Current Setpoint"
when
	Item GF_Heating received update
then
    setGFTemp = GF_Heating.state
    logInfo("temperature", "DFTemperature change: {}", setGFTemp)

    sendCommand(GF_Temp_Current_Setpoint, setGFTemp)
    analyzeTemperature.apply(HEAT_GFPump, GF_Temperature, setGFTemp)
end


rule "Check Heating items"
when
	Time cron "0 0/1 * * * ?"
then
    if(GF_Heating.state == NULL) {
        logInfo("temperature", "GF_Heating is NULL. Setting temp to: {}", setGFTemp)
        sendCommand(GF_Heating, setGFTemp)
    }

    if(FF_Heating.state == NULL) {
        logInfo("temperature", "FF_Heating is NULL. Setting temp to: {}", setFFTemp)
        sendCommand(FF_Heating, setFFTemp)
    }
end